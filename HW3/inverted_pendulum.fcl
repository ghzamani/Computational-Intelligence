FUNCTION_BLOCK Inverted_Pendulum

    VAR_INPUT
        x : REAL; 
        x_dot : REAL; 
        x_dot_dot : REAL; 
        theta : REAL;
        theta_dot : REAL;        
        theta_dot_dot : REAL; 
    END_VAR

    VAR_OUTPUT
        f : REAL;
    END_VAR

    FUZZIFY x
        TERM left_corner := (0, 0) (10, 1) (20, 0);
        TERM left := (10, 0) (20, 1) (30, 0);
        TERM middle := (20, 0) (30, 1) (40, 0);
        TERM right := (30, 0) (40, 1) (50, 0);
        TERM right_corner :=  (40, 0) (50, 1) (60, 0);
    END_FUZZIFY

    FUZZIFY x_dot
        (* terms *)
        TERM left := (-20, 0) (-10, 1) (0, 0);
        TERM zero := (-10, 0) (0, 1) (10, 0);
        TERM right := (0, 0) (10, 1) (20, 0);
    END_FUZZIFY

    FUZZIFY theta
        TERM down_right := (0, 0) (45, 1) (90, 0);
        TERM middle_right := (80,0) (135,1) (170,0);
        TERM up_right := (165,0) (173,1) (179,0);
        TERM stable_right := (177,0) (178,1) (179,0);
        TERM stable_left := (181,0) (182,1) (183,0);
        TERM up_left := (183,0) (186.5,1) (190,0);
        TERM middle_left := (225,0) (270,1) (315,0);
        TERM down_left := (270,0) (315,1) (360,0);
    END_FUZZIFY

    FUZZIFY theta_dot
        TERM clk_high := (-150,0) (-10,1) (-5,0);
        TERM clk := (-10,0) (-5,1) (0,0);
        TERM clk_low := (-6,0) (-3,1) (0,0);
        TERM zero := (-1,0) (0,1) (1,0);
        TERM anti_clk_low := (0,0) (3,1) (6,0);
        TERM anti_clk := (0,0) (5,1) (10,0);
        TERM anti_clk_high := (5,0) (10, 1) (150, 0);
    END_FUZZIFY

    DEFUZZIFY f
        TERM left_very_high := (-120,0) (-60,1) (0,0);
        TERM left_high := (-70,0) (-35,1) (0,0);
        TERM left_low := (-30,0) (-15,1) (0,0);
        TERM left_very_low := (-10,0) (-5,1) (0,0);
        TERM zero := (-5,0) (0,1) (5,0);
        TERM right_very_low := (0,0) (5,1) (10,0);
        TERM right_low := (0,0) (15,1) (30,0);
        TERM right_high := (0,0) (35,1) (70,0);
        TERM right_very_high := (0,0) (60,1) (120,0);
        
        METHOD: COG;
        DEFAULT := 0;
    END_DEFUZZIFY

    RULEBLOCK Inverted_Pendulum
        AND : AlgebraicProduct;
       
        RULE 1: IF (theta IS down_left) AND (theta_dot IS anti_clk_low) THEN f IS left_very_high;      
        RULE 2: IF (theta IS down_right) AND (theta_dot IS clk_low) THEN f IS right_very_high;       

        RULE 3: IF (theta IS up_left) AND (theta_dot IS anti_clk) THEN f IS left_very_high;
        RULE 4: IF (theta IS up_right) AND (theta_dot IS clk) THEN f IS right_very_high;

        RULE 5: IF (theta IS stable_left) AND (theta_dot IS anti_clk) THEN f IS left_low;
        RULE 6: IF (theta IS stable_right) AND (theta_dot IS clk) THEN f IS right_low;
    END_RULEBLOCK

END_FUNCTION_BLOCK